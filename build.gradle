/*
 * Copyright (C) 2013 JFrog Ltd.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 * http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */
 buildscript {
    repositories {
        maven {
            url "https://cahltech.jfrog.io/artifactory/test-gradle-release"
            credentials  {
                username = System.getenv("ARTIFACTORY_USER")
                password = System.getenv("ARTIFACTORY_PASSWORD")
            }
        }
    }
    dependencies {
        classpath(group: 'org.jfrog.buildinfo', name: 'build-info-extractor-gradle', version: '4.26.3')
    }
}

allprojects {
    repositories {
        maven {
            url "https://cahltech.jfrog.io/artifactory/test-gradle-release"
            credentials  {
                username = System.getenv("ARTIFACTORY_USER")
                password = System.getenv("ARTIFACTORY_PASSWORD")
            }
        }
    }
}

version = currentVersion

apply plugin: 'com.jfrog.artifactory'
apply plugin: 'java'
apply plugin: 'maven-publish'

dependencies {
    testImplementation 'junit:junit:4.7'
}

task configureWrapper {
    String username = System.getProperty("gradle.wrapperUser")
    String password = System.getProperty("gradle.wrapperPassword")
    String gradleVersion = '7.2'
    println "https://${username}:${password}@cahltech.jfrog.io/artifactory/gradle-distributions/gradle-${gradleVersion}-all.zip"
    doLast {
        wrapper.distributionUrl = "https://${username}:${password}@cahltech.jfrog.io/artifactory/gradle-distributions/gradle-${gradleVersion}-all.zip"
    }
}

wrapper.dependsOn configureWrapper

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
        }
    }
}

artifactory {
    contextUrl = 'https://cahltech.jfrog.io/artifactory'
    publish {
        repository {
            repoKey = 'test-gradle-dev-local' // The Artifactory repository key to publish to
            username = System.getenv("ARTIFACTORY_USER") // The publisher user name
            password = System.getenv("ARTIFACTORY_PASSWORD") // The publisher password
        }
        defaults {
            // Reference to Gradle publications defined in the build script.
            // This is how we tell the Artifactory Plugin which artifacts should be
            // published to Artifactory.
            publications('mavenJava')
            publishArtifacts = true
            // Properties to be attached to the published artifacts.
            properties = ['qa.level': 'basic', 'dev.team' : 'core']
            // Publish generated POM files to Artifactory (true by default)
            publishPom = true
        }
    }
}
